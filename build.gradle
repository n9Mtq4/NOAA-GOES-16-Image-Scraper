import proguard.gradle.ProGuardTask

buildscript {
    ext.kotlin_version = '1.3.21'
    
    repositories {
        mavenCentral()
        flatDir dirs: proguard_dir
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
        classpath ':proguard:'
    }
}

group 'NOAA-Goes-16-Image-Scraper'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

sourceCompatibility = 1.8
mainClassName = "com.n9mtq4.goes16scraper.ScraperKt"

apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.1.1"
    implementation "commons-cli:commons-cli:1.4"
    implementation "org.jsoup:jsoup:1.11.3"
    implementation "com.googlecode.json-simple:json-simple:1.1.1"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    exclude 'LICENSE.txt'
    exclude '**/*.kotlin_metadata'
    exclude '**/*.kotlin_builtins'
    dependencies {
        exclude(dependency(':junit'))
        exclude(dependency('org.hamcrest:'))
        exclude(dependency(':hamcrest-core'))
    }
}

task proguard(type: ProGuardTask) {
    
    configuration 'proguard.txt'
    
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    
    injars "build/libs/${project.getName()}-${project.getVersion()}.jar"
    outjars "build/libs/${project.getName()}-${project.getVersion()}-proguard.jar"
    
}
